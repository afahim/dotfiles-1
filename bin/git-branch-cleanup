#!/usr/bin/env perl

=head1 NAME

    git-branch-cleanup - remove local or remote dead branches

=head1 SYNOPSIS

    git-branch-cleanup [options] [remote ...]

     Options:
       --all            Delete all non-blacklisted branches, non-interactively.
       --blacklist      Add the next string in the command-line to the blacklist.
                            Contains 'master' and 'HEAD' no matter what.
       --dry_run|-n     Do not execute removal commands, just print what would have been done.
       --help|-?        This help message.
       --interactive    Interactively approve the branch to be deleted.
                            Turned on by default.
       --remotes|-r     Do this on the selected remotes.
                            Defaults to origin.
       --verbose        Print extra information.

=cut


use warnings;
use strict;

use Getopt::Long;
use Pod::Usage qw/pod2usage/;

our $BLACKLIST = ['master', 'HEAD'];
our $GIT_BRANCH_COMMAND = 'git branch';
our @REMOTES = q{origin};
our $REMOTES = qr{(?!)};

our($do_remotes, $dry_run, $interactive, $verbose);
$interactive = 1;

GetOptions(
           'all'           => sub { $interactive = 0; },
           'blacklist=s@'  => \$BLACKLIST,
           'dry_run|n'     => \$dry_run,
           'help|?'        => sub { pod2usage(); },
           'interactive'   => \$interactive,
           'remotes'       => \$do_remotes,
           'verbose'       => \$verbose,
);

if( @ARGV ){
    @REMOTES = @ARGV;
}

make_blacklist_regex();

if( $do_remotes ){
    make_remotes_regex();
    $GIT_BRANCH_COMMAND = 'git branch -r';
}

open BRANCHES_FH, "$GIT_BRANCH_COMMAND |" or die "Couldn't open '$GIT_BRANCH_COMMAND': $!";

my $branch;
while( $branch = <BRANCHES_FH> ){
    $branch =~ s/\A\s+|\s+\Z//g;

    print "Processing branch $branch\n" if $verbose;
    next if $branch =~ /^\s*\*/;
    next if $do_remotes and $REMOTES and $branch !~ $REMOTES;
    next if $BLACKLIST and $branch =~ $BLACKLIST;

    if( $interactive ){
        print "Delete branch '$branch'? [Y/n/q] ";
        my $input = <STDIN>;
        exit 0 if $input =~ /^\s*q/i;
        next if $input =~ /^\s*n/i;
    }

    if( $do_remotes ){
        my($remote, $branch_name) = $branch =~ m{(\S*)/(\S*)};
        print "git push $remote :$branch_name\n" if $verbose or $dry_run;
        system("git push $remote :$branch_name") unless $dry_run;
    }
    else {
        print "git branch -D $branch" if $verbose or $dry_run;
        system("git branch -D $branch") unless $dry_run;
    }
}

sub make_remotes_regex {
    $REMOTES = join '|', map { "^$_/" } @REMOTES;
    print "\$REMOTES is '$REMOTES'\n" if $verbose;
    $REMOTES = qr{$REMOTES};
}

sub make_blacklist_regex {
    if( $do_remotes ){
        $BLACKLIST = join '|', map{ "\\S+/$_" } @$BLACKLIST;
    }
    else {
        $BLACKLIST = join '|', @$BLACKLIST;
    }
    print "\$BLACKLIST is '$BLACKLIST'\n" if $verbose;
    $BLACKLIST = qr{$BLACKLIST};
}
